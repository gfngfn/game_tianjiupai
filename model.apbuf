@language_version "0.0.2"
@output "elm": {
  dir     = "./src_client/_generated",
  module  = "Models",
  imports = [],
}

user_id := string

user_name := string

user := {
  user_id   : user_id,
  user_name : user_name,
}

room_id := string

room_name := string

room := {
  room_id   : room_id,
  room_name : room_name,
}

flag_user := {
  id         : user_id,
  name       : user_name,
  belongs_to : option(room_id),
}

flag := {
  user : option(flag_user),
}

create_user_request := {
  user_name : user_name
}

create_user_response := {
  user_id : user_id
}

create_room_request := {
  user_id   : user_id,
  room_name : room_name,
}

create_room_response := {
  room_id : room_id,
}

get_all_rooms_response := {
  rooms : list(room_summary),
}

get_room_response :=
  personal_state

enter_room_request := {
  user_id : user_id
}

enter_room_response :=
  personal_state

room_summary := {
  room       : room,
  members    : list(user_id),
  is_playing : bool,
}

comment := {
  from : user_id,
  text : string,
}

log :=
  | LogComment : comment
  | LogEntered : user_id
  | LogExited  : user_id
  | LogGameStart

notification :=
  | NotifyComment    : comment
  | NotifyEntered    : user_id
  | NotifyExited     : user_id
  | NotifyGameStart  : observable_game_state
  | NotifyNextStep
  | NotifySubmission : submission

submission := {
    snapshot_id : snapshot_id,
    seat        : seat,
    submitted   : list(option(card)),
    new_state   : observable_game_state,
}

command :=
  | CommandComment : string
  | CommandAck     : snapshot_id

personal_state := {
  room : room,
  logs : list(log),
  game : personal_room_state,
}

personal_room_state :=
  | WaitingStart : list(user_id)
  | PlayingGame  : observable_game_state

per_seat($a) := {
  east  : $a,
  south : $a,
  west  : $a,
  north : $a,
}

card_wen := int

card_wu := int

card_big := int

card :=
  | Wen : card_wen
  | Wu  : card_wu

closed_or($a) :=
  | Open : $a
  | Closed

exposed($a) := {
  first      : $a,
  subsequent : closed_or($a),
}

unit :=
  | Unit

table :=
  | Starting
  | TrickWuzun      : exposed(unit)
  | TrickWenzun     : exposed(bool)
  | TrickSingleWen  : exposed(card_wen)
  | TrickSingleWu   : exposed(card_wu)
  | TrickDoubleWen  : exposed(card_wen)
  | TrickDoubleWu   : exposed(card_wu)
  | TrickDoubleBoth : exposed(card_big)
  | TrickTripleWen  : exposed(card_big)
  | TrickTripleWu   : exposed(card_big)
  | TrickQuadruple  : exposed(card_big)

seat := int

snapshot_id := string

game_player := {
  user_id : user_id,
  score   : int,
}

game_meta := {
  inning_index     : int,
  num_consecutives : int,
  parent_seat      : seat,
  players          : per_seat(game_player),
}

observable_inning_state := {
  starts_at : seat,
  your_hand : list(card),
  gains     : per_seat(list(card)),
  table     : table,
}

observable_inning :=
  | ObservableDuringInning : observable_inning_state
  | ObservableInningEnd    : per_seat(list(card))

observable_game_state := {
  meta              : game_meta,
  observable_inning : observable_inning,
  synchronizing     : bool,
  snapshot_id       : snapshot_id,
}
