import Quad
import Card
import Types
import Inning

module InningTests = #[test] struct
  open Stdlib
  open Types

  #[test]
  val all_cards_test() =
    Testing.it("The number of all cards is 32.", fun() ->
      assert Testing.equal(
        -expect 32,
        -got List.length(Inning.all_cards())
      )
    end)

  #[test]
  val shuffle_test() =
    Testing.it("Every generated hand consists of eight cards.", fun() ->
      let quad = Inning.shuffle() in
      assert Testing.equal(
        -expect {8, 8, 8, 8},
        -got Quad.map(List.length, quad))
    end)

  val wu_number(b, wunum) =
    { design = b, number = wunum }

  val wu(b, wunum) =
    Card.Wu(wu_number(b, wunum))

  val bigd(b, big) : big_with_design =
    { design = b, main = big }

  #[test]
  val make_starting_table_test() =
    let first(x) = {x, []} in
    let ok(x) = Some(x) in
    let error() = None in
    Testing.list(
      List.map(
        fun(test_case) ->
          let {submitted_cards, expected} = test_case in
          Testing.it("Makes a starting table", fun() ->
            assert Testing.equal(
              -expect expected,
              -got Inning.make_starting_table(submitted_cards)
            )
          end)
        end,
        List.append(
          List.map(
            fun(test_case) ->
              let {submitted_cards, expected_table} = test_case in
              {submitted_cards, ok(expected_table)}
            end,
            [
              {[Card.Wen(11)],
                 SingleWen(first(11))},

              {[wu(true, 8)],
                 SingleWu(first(wu_number(true, 8)))},

              {[Card.Wen(4), Card.Wen(4)],
                 DoubleWen(first(4))},

              {[wu(false, 7), wu(true, 7)],
                 DoubleWu(first(7))},

              {[Card.Wen(1), Card.Wen(1)],
                 Wenzun(first(Minor))},

              {[wu(true, 3), wu(true, 6)],
                 Wuzun(first({}))},

              {[Card.Wen(11), wu(true, 9)],
                 DoubleBoth(first(bigd(true, Card.Big4)))},
              {[Card.Wen(10), wu(false, 8)],
                 DoubleBoth(first(bigd(false, Card.Big3)))},
              {[Card.Wen(9), wu(false, 7)],
                 DoubleBoth(first(bigd(false, Card.Big2)))},
              {[Card.Wen(8), wu(true, 5)],
                 DoubleBoth(first(bigd(true, Card.Big1)))},

              {[wu(false, 9), Card.Wen(11)],
                 DoubleBoth(first(bigd(false, Card.Big4)))},
              {[wu(true, 8), Card.Wen(10)],
                 DoubleBoth(first(bigd(true, Card.Big3)))},
              {[wu(true, 7), Card.Wen(9)],
                 DoubleBoth(first(bigd(true, Card.Big2)))},
              {[wu(false, 5), Card.Wen(8)],
                 DoubleBoth(first(bigd(false, Card.Big1)))},

              {[Card.Wen(11), wu(true, 9), Card.Wen(11)],
                 TripleWen(first(bigd(true, Card.Big4)))},
              {[wu(false, 8), Card.Wen(10), Card.Wen(10)],
                 TripleWen(first(bigd(false, Card.Big3)))},
              {[Card.Wen(9), Card.Wen(9), wu(true, 7)],
                 TripleWen(first(bigd(true, Card.Big2)))},
              {[Card.Wen(8), wu(true, 5), Card.Wen(8)],
                 TripleWen(first(bigd(true, Card.Big1)))},
            ]),
          [
            {[],
               error()},
            {[Card.Wen(4), Card.Wen(5)],
               error()},
            {[Card.Wen(11), wu(true, 8)],
               error()}
          ])))
end
