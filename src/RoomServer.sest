import Quad
import Card
import SnapshotId
import Types
import Inning

module RoomServer :> sig
  open Stdlib
  open Types

  type info :: o
  type proc :: o
  val start_link<$a> : fun(room_id, binary) -> [$a]result<proc, GenServer.start_link_error>
  val as_pid : fun(proc) -> pid<info>
  val from_pid : fun(pid<info>) -> proc
  val monitor<$a> : fun(room_id) -> [$a]option<RawValue.t>

  val get_whole_state<$a> : fun(room_id) -> [$a]option<whole_room_state>
  val get_whole_state_by_proc<$a> : fun(proc) -> [$a]option<whole_room_state>
  val get_personal_state<$a> : fun(room_id, user_id) -> [$a]option<personal_room_state>

  val send_chat<$a> : fun(room_id, user_id, binary) -> [$a]option<unit>
  val attend<$a> : fun(room_id, user_id) -> [$a]option<personal_room_state>
  val exit<$a> : fun(room_id, user_id) -> [$a]option<unit>
  val submit<$a> : fun(room_id, user_id, list<Card.t>) -> [$a]option<snapshot_id>
  val ack<$a> : fun(room_id, user_id, snapshot_id) -> [$a]option<next_step>

end = struct
  open Stdlib
  open Types

  val todo : fun(binary, int) -> unit = external 2 ```
    todo(Filename, Line) ->
      erlang:error({todo, Filename, Line}).
  ```

  type settings = {
    room_id   : room_id,
    room_name : binary,
  }

  type waiting_state = {
    members : list<user_id>,
  }

  type sync =
    | WaitingInput
    | WaitingSync(Quad.t<bool>)

  type whole_game_state = {
    meta        : game_meta,
    inning      : Inning.t,
    snapshot_id : snapshot_id,
    sync        : sync, /* Tracks whether each player is ready for the next step. */
  }

  type internal_room_state =
    | InternalWaiting(waiting_state)
    | InternalPlaying(whole_game_state)

  type request_impl =
    | GetWholeState
    | GetPersonalState(user_id)
    | SendChat(user_id, binary)
    | Attend(user_id)
    | Exit(user_id)
    | Ack(user_id, snapshot_id)
    | Submit(user_id, list<Card.t>)

  type response_impl =
    | WholeState(whole_room_state)
    | PersonalState(option<personal_room_state>)
    | ChatSent
    | Attended(option<personal_room_state>)
    | Exited(bool)
    | SynAck(option<next_step>)
    | SubmissionDone(option<snapshot_id>)

  module Callback = struct
    type init_arg = {room_id, binary}

    type request = request_impl

    type response = response_impl

    type cast_message =
      | DummyCastMessage

    type info =
      | DummyInfo

    type state = {
      settings      : settings,
      reversed_logs : list<log>,
      room_state    : internal_room_state,
    }

    type global = room_id

    type stop_reason = RawValue.t

    val init(init_arg) = act
      let {room_id, room_name} = init_arg in
      let settings = {room_id = room_id, room_name = room_name} in
      GenServer.init_ok({
        settings      = settings,
        reversed_logs = [],
        room_state    = InternalWaiting({members = []})
      })

    val get_members_from_state(room_state : internal_room_state) : {bool, list<user_id>} =
      case room_state of
      | InternalWaiting(waiting_state) ->
          {false, waiting_state.members}

      | InternalPlaying(whole_game_state) ->
          let game_players = Quad.to_list(whole_game_state.meta.players) in
          let members = List.map(fun(g) -> g.user_id end, game_players) in
          {true, members}
      end

    val make_whole_room_state(state : state) : whole_room_state =
      let settings = state.settings in
      let {is_playing, members} = get_members_from_state(state.room_state) in
      {
        room_id    = settings.room_id,
        room_name  = settings.room_name,
        is_playing = is_playing,
        members    = members,
      }

    val find_user_seat(game_state : whole_game_state, user_id : user_id) =
      let opt = Quad.find(fun(p) -> Binary.equal(p.user_id, user_id) end, game_state.meta.players) in
      Option.map(
        fun(pair) ->
          let {seat, _} = pair in
          seat
        end,
        opt)

    val the_other_players(game_state : whole_game_state, seat : Quad.seat) : list<user_id> =
      let {u0, u1, u2, u3} = Quad.map(fun(p) -> p.user_id end, game_state.meta.players) in
      case seat of
      | Quad.Seat0 -> [u1, u2, u3]
      | Quad.Seat1 -> [u2, u3, u0]
      | Quad.Seat2 -> [u3, u0, u1]
      | Quad.Seat3 -> [u0, u1, u2]
      end

    val make_observable_game_state(game_state : whole_game_state, user_id : user_id) : option<observable_game_state> =
      case find_user_seat(game_state, user_id) of
      | None ->
          None

      | Some(seat) ->
          let synchronizing =
            case game_state.sync of
            | WaitingInput   -> false
            | WaitingSync(_) -> true
            end
          in
          let observable_inning = Inning.observe(seat, game_state.inning) in
          Some({
            meta              = game_state.meta,
            observable_inning = observable_inning,
            synchronizing     = synchronizing,
            snapshot_id       = game_state.snapshot_id,
          })
      end

    val make_observable(room_state : internal_room_state, user_id : user_id) : option<observable_room_state> =
      case room_state of
      | InternalWaiting(waiting_state) ->
          Some(Waiting(waiting_state.members))

      | InternalPlaying(game_state) ->
          Option.map(fun(obs) -> Playing(obs) end, make_observable_game_state(game_state, user_id))
      end

    val make_personal_room_state(state : state, user_id : user_id) : option<personal_room_state> =
      let settings = state.settings in
      case make_observable(state.room_state, user_id) of
      | None ->
          None

      | Some(observable) ->
          Some({
            room_id    = settings.room_id,
            room_name  = settings.room_name,
            logs       = List.reverse(state.reversed_logs),
            observable = observable,
          })
      end

    val make_initial_sync(seat) =
      Quad.update(seat, true, {false, false, false, false})

    val notify<$a> : fun(user_id, list<notification>) -> [$a]unit = external 2 ```
      notify(UserId, Notifications) ->
          case tianjiupai_websocket:notify(UserId, Notifications) of
              ok ->
                  ok;
              {error, Reason} ->
                  io:format("~p, notify_log failed (reason: ~p, to: ~p, notifications: ~p)",
                      [?MODULE, Reason, UserId, Notifications]),
                  ok
          end.
    ```

    val notify_for_each<$a>(user_ids : list<user_id>, logs : list<notification>) : [$a]unit = act
      List.for_each(fun(user_id) -> act notify(user_id, logs) end, user_ids)

    val handle_send_chat(user_id : user_id, text : binary, state : state) = act
      let {_, members} = get_members_from_state(state.room_state) in
      do dummy <- notify_for_each(members, [NotifyComment(user_id, text)]) in
      GenServer.reply(ChatSent, { state | reversed_logs = LogComment(user_id, text) :: state.reversed_logs })

    val handle_attend(user_id : user_id, state : state) = act
      case state.room_state of
      | InternalPlaying(_) ->
          GenServer.reply(Attended(None), state)

      | InternalWaiting(waiting_state) ->
          let members = waiting_state.members in
          if List.any(fun(u) -> Binary.equal(u, user_id) end, members) then
            let Some(personal_state) = make_personal_room_state(state, user_id) in
            GenServer.reply(Attended(Some(personal_state)), state)
          else
            let log_enter = LogEntered(user_id) in
            let notif_enter = NotifyEntered(user_id) in
            case members of
            | u0 :: u1 :: u2 :: _ ->
                let player_quad =
                  let player0 = { user_id = u0, score = 0 } in
                  let player1 = { user_id = u1, score = 0 } in
                  let player2 = { user_id = u2, score = 0 } in
                  let player3 = { user_id = user_id, score = 0 } in
                  {player0, player1, player2, player3}
                in
                let parent_seat = Quad.Seat0 in
                let game_meta =
                  {
                    inning_index     = 0,
                    num_consecutives = 1,
                    parent_seat      = parent_seat,
                    players          = player_quad,
                  }
                in
                do snapshot_id <- SnapshotId.generate() in
                let inning = Inning.start(parent_seat) in
                let game_state =
                  {
                    meta        = game_meta,
                    inning      = inning,
                    snapshot_id = snapshot_id,
                    sync        = WaitingSync(make_initial_sync(Quad.Seat3)),
                  }
                  /* The player who lastly attended the room is ready for the next step from the beginning. */
                in
                let Some(obs0) = make_observable_game_state(game_state, u0) in
                let Some(obs1) = make_observable_game_state(game_state, u1) in
                let Some(obs2) = make_observable_game_state(game_state, u2) in
                do dummy <- notify(u0, [notif_enter, NotifyGameStart(obs0)]) in
                do dummy <- notify(u1, [notif_enter, NotifyGameStart(obs1)]) in
                do dummy <- notify(u2, [notif_enter, NotifyGameStart(obs2)]) in
                let state =
                  { state |
                    room_state    = InternalPlaying(game_state),
                    reversed_logs = LogGameStart :: log_enter :: state.reversed_logs,
                  }
                in
                let Some(personal_state) = make_personal_room_state(state, user_id) in
                GenServer.reply(Attended(Some(personal_state)), state)

            | _ ->
                let waiting_state = { waiting_state | members = List.append(members, [user_id]) } in
                do dummy <- notify_for_each(members, [notif_enter]) in
                let state =
                  { state |
                    room_state    = InternalWaiting(waiting_state),
                    reversed_logs = log_enter :: state.reversed_logs,
                  }
                in
                let Some(personal_state) = make_personal_room_state(state, user_id) in
                GenServer.reply(Attended(Some(personal_state)), state)
            end
      end

    val handle_exit(user_id : user_id, state : state) = act
      case state.room_state of
      | InternalPlaying(_) ->
          GenServer.reply(Exited(false), state)

      | InternalWaiting(waiting_state) ->
          let members =
            List.filter(
              fun(u) -> Bool.not(Binary.equal(u, user_id)) end,
              waiting_state.members)
          in
          let room_state = InternalWaiting({ waiting_state | members = members }) in
          GenServer.reply(Exited(true), { state | room_state = room_state })
      end

    val handle_submit(user_id : user_id, cards : list<Card.t>, state : state) = act
      do opt <-
        case state.room_state of
        | InternalWaiting(_) ->
            return(None)

        | InternalPlaying(game_state) ->
            case find_user_seat(game_state, user_id) of
            | None ->
                return(None)

            | Some(seat) ->
                case Inning.submit(seat, cards, game_state.inning) of
                | None ->
                    return(None)

                | Some(submit_result) ->
                    do snapshot_id <- SnapshotId.generate() in
                    let game_state =
                      case submit_result of
                      | Inning.Continues(inning) ->
                          { game_state |
                            inning = inning,
                            snapshot_id = snapshot_id,
                            sync        = WaitingSync(make_initial_sync(seat)),
                          }

                      | Inning.WinsTrick(winner_seat, inning) ->
                          { game_state |
                            inning      = inning,
                            snapshot_id = snapshot_id,
                            sync        = WaitingSync(make_initial_sync(seat)),
                          }

                      | Inning.WinsInning(winner_seat, gains_quad) ->
                          let _ = assert todo in game_state
                      end
                    in
                    let user_ids = the_other_players(game_state, seat) in
                    do dummy <- notify_for_each(user_ids, [NotifySubmission(snapshot_id, seat, cards)]) in
                    return(Some({snapshot_id, game_state}))
                end
            end
        end
      in
      case opt of
      | None ->
          GenServer.reply(SubmissionDone(None), state)

      | Some({snapshot_id, game_state}) ->
          GenServer.reply(SubmissionDone(Some(snapshot_id)), { state | room_state = InternalPlaying(game_state) })
      end

    val handle_ack(user_id : user_id, snapshot_id : snapshot_id, state : state) = act
      do opt <-
        case state.room_state of
        | InternalWaiting(_) ->
            return(None)

        | InternalPlaying(game_state) ->
            case find_user_seat(game_state, user_id) of
            | None ->
                return(None)

            | Some(seat) ->
                if SnapshotId.equal(snapshot_id, game_state.snapshot_id) then
                  return(None)
                else
                  case game_state.sync of
                  | WaitingInput ->
                      return(None)

                  | WaitingSync(sync_quad) ->
                      let sync_quad = Quad.update(seat, true, sync_quad) in
                      case sync_quad of
                      | {true, true, true, true} ->
                        /* If all the players are ready for the next step: */
                          let user_ids = the_other_players(game_state, seat) in
                          do dummy <- notify_for_each(user_ids, [NotifyNextStep]) in
                          let game_state = { game_state | sync = WaitingInput } in
                          return(Some({Transition, game_state}))

                      | _ ->
                          let game_state = { game_state | sync = WaitingSync(sync_quad) } in
                          return(Some({StillWaiting, game_state}))
                      end
                  end
            end
        end
      in
      case opt of
      | None ->
          GenServer.reply(SynAck(None), state)

      | Some({next_step, game_state}) ->
          GenServer.reply(SynAck(Some(next_step)), { state | room_state = InternalPlaying(game_state) })
      end

    val handle_call(req, from, state) = act
      case req of
      | GetWholeState ->
          let whole_state = make_whole_room_state(state) in
          GenServer.reply(WholeState(whole_state), state)
      | GetPersonalState(user_id) ->
          let personal_state = make_personal_room_state(state, user_id) in
          GenServer.reply(PersonalState(personal_state), state)
      | SendChat(user_id, text) ->
          handle_send_chat(user_id, text, state)
      | Attend(user_id) ->
          handle_attend(user_id, state)
      | Exit(user_id) ->
          handle_exit(user_id, state)
      | Ack(user_id, snapshot_id) ->
          handle_ack(user_id, snapshot_id, state)
      | Submit(user_id, cards) ->
          handle_submit(user_id, cards, state)
      end

    val handle_cast(msg, state) = act
      let _ = print_debug({"unexpected cast message", msg}) in
      GenServer.no_reply(state)

    val handle_info(info, state) = act
      let _ = print_debug({"unexpected info", info}) in
      GenServer.no_reply(state)

    val terminate(state) = act
      let _ = print_debug({"terminate", state}) in
      return({})
  end

  module Impl = GenServer.Make(Callback)

  type info = Callback.info

  type proc = Impl.proc

  val monitor_impl<$a> : fun(pack sig end, room_id) -> [$a]option<RawValue.t> = external 2 ```
    monitor_impl(Impl, RoomId) ->
        case Impl:where_is_global(RoomId) of
            error     -> error;
            {ok, Pid} -> {ok, erlang:monitor(process, Pid)}
        end.
  ```

  val monitor(room_id) = act
    monitor_impl(pack Impl : sig end, room_id)

  val start_link<$a>(room_id : room_id, room_name : binary) : [$a]result<proc, GenServer.start_link_error> = act
    Impl.start_link_name({room_id, room_name}, -name Global(room_id))

  val as_pid(proc) =
    Impl.as_pid(proc)

  val from_pid(proc) =
    Impl.from_pid(proc)

  val call<$a, $ret>(
    room_id : room_id,
    req     : request_impl,
    f       : fun(response_impl) -> [$a]option<$ret>
  ) : [$a]option<$ret> = act
    do proc_opt <- Impl.where_is_global(room_id) in
    case proc_opt of
    | None ->
        return(None)

    | Some(proc) ->
        do resp <- Impl.call(proc, req) in
        f(resp)
    end

  val get_whole_state<$a>(room_id : room_id) : [$a]option<whole_room_state> = act
    call(room_id, GetWholeState, fun(resp) -> act
      case resp of
      | WholeState(whole_state) -> return(Some(whole_state))
      end
    end)

  val get_whole_state_by_proc<$a>(proc : proc) : [$a]option<whole_room_state> = act
    do resp <- Impl.call(proc, GetWholeState) in
    case resp of
    | WholeState(whole_state) -> return(Some(whole_state))
    end

  val get_personal_state<$a>(room_id : room_id, user_id : user_id) : [$a]option<personal_room_state> = act
    call(room_id, GetPersonalState(user_id), fun(resp) -> act
      case resp of
      | PersonalState(personal_state_opt) -> return(personal_state_opt)
      end
    end)

  val send_chat<$a>(room_id, user_id, text) : [$a]option<unit> = act
    call(room_id, SendChat(user_id, text), fun(resp) -> act
      case resp of
      | ChatSent -> return(Some({}))
      end
    end)

  val attend<$a>(room_id, user_id) : [$a]option<personal_room_state> = act
    call(room_id, Attend(user_id), fun(resp) -> act
      case resp of
      | Attended(game_state_opt) -> return(game_state_opt)
      end
    end)

  val exit<$a>(room_id, user_id) : [$a]option<unit> = act
    call(room_id, Exit(user_id), fun(resp) -> act
      case resp of
      | Exited(false) -> return(None)
      | Exited(true)  -> return(Some({}))
      end
    end)

  val submit<$a>(room_id, user_id, cards) : [$a]option<snapshot_id> = act
    call(room_id, Submit(user_id, cards), fun(resp) -> act
      case resp of
      | SubmissionDone(snapshot_id_opt) -> return(snapshot_id_opt)
      end
    end)

  val ack<$a>(room_id, user_id, snapshot_id) : [$a]option<next_step> = act
    call(room_id, Ack(user_id, snapshot_id), fun(resp) -> act
      case resp of
      | SynAck(None)            -> return(None)
      | SynAck(Some(next_step)) -> return(Some(next_step))
      end
    end)
end
