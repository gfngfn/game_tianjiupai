import Logger
import Models
import Quad
import Card
import SnapshotId
import Inning

module RoomServer :> sig
  open Stdlib
  open Models

  type info :: o
  type proc :: o
  val start_link<$a> : fun(room_id, binary) -> [$a]result<proc, GenServer.start_link_error>
  val as_pid : fun(proc) -> pid<info>
  val from_pid : fun(pid<info>) -> proc
  val monitor<$a> : fun(room_id) -> [$a]option<RawValue.t>

  val get_whole_state<$a> : fun(room_id) -> [$a]option<room_summary>
  val get_whole_state_by_proc<$a> : fun(proc) -> [$a]option<room_summary>
  val get_personal_state<$a> : fun(room_id, user_id) -> [$a]option<personal_state>

  val send_chat<$a> : fun(room_id, user, binary) -> [$a]option<unit>
  val attend<$a> : fun(room_id, user) -> [$a]option<personal_state>
  val exit<$a> : fun(room_id, user_id) -> [$a]option<unit>
  val submit<$a> : fun(room_id, user_id, list<Card.t>) -> [$a]option<{observable_game_state, option<last>}>
  val ack<$a> : fun(room_id, user_id, snapshot_id) -> [$a]unit
  val require_next_inning<$a> : fun(room_id, user_id, snapshot_id) -> [$a]unit
  val notify_connected<$a> : fun(room_id, user_id) -> [$a]unit
  val notify_disconnected<$a> : fun(room_id, user_id) -> [$a]unit

end = struct
  open Stdlib
  open Models

  val todo<$a, $b> : fun($a) -> $b = external 1 ```
    todo(X) ->
      erlang:error({todo, X}).
  ```

  type settings = {
    room_id   : room_id,
    room_name : binary,
  }

  type waiting_member = {
    user         : user,
    is_connected : bool,
  }

  type waiting_state = {
    members : list<waiting_member>,
  }

  type sync =
    | WaitingInput
    | WaitingSync(Quad.t<bool>)

  type whole_inning =
    | DuringInning(Inning.t)
    | InningEnd(Quad.t<list<Card.t>>, Quad.t<bool>)

  type whole_game_state = {
    meta        : game_meta,
    inning      : whole_inning,
    snapshot_id : snapshot_id,
    sync        : sync, /* Tracks whether each player is ready for the next step. */
  }

  type internal_room_state =
    | InternalWaiting(waiting_state)
    | InternalPlaying(whole_game_state)

  type request_impl =
    | GetWholeState
    | GetPersonalState(user_id)
    | SendChat(user, binary)
    | Attend(user)
    | Exit(user_id)
    | Submit(user_id, list<Card.t>)

  type response_impl =
    | WholeState(room_summary)
    | PersonalState(option<personal_state>)
    | ChatSent
    | Attended(option<personal_state>)
    | Exited(bool)
    | SubmissionDone(option<{observable_game_state, option<last>}>)

  module Callback = struct
    type init_arg = {room_id, binary}

    type request = request_impl

    type response = response_impl

    type cast_message =
      | Ack(user_id, snapshot_id)
      | RequireNextInning(user_id, snapshot_id)
      | SetConnectionStatus(user_id, bool)

    type info =
      | DummyInfo

    type state = {
      settings      : settings,
      reversed_logs : list<log>,
      room_state    : internal_room_state,
    }

    type global = room_id

    type stop_reason = RawValue.t

    val init(init_arg) = act
      let {room_id, room_name} = init_arg in
      let settings = {room_id = room_id, room_name = room_name} in
      GenServer.init_ok({
        settings      = settings,
        reversed_logs = [],
        room_state    = InternalWaiting({members = []})
      })

    val get_members_from_state(room_state : internal_room_state) : {bool, list<user>} =
      case room_state of
      | InternalWaiting(waiting_state) ->
          {false, List.map(fun(w) -> w.user end, waiting_state.members)}

      | InternalPlaying(whole_game_state) ->
          let game_players = Quad.to_list(whole_game_state.meta.players) in
          let members = List.map(fun(p) -> p.user end, game_players) in
          {true, members}
      end

    val make_whole_room_state(state : state) : room_summary =
      let settings = state.settings in
      let {is_playing, members} = get_members_from_state(state.room_state) in
      {
        room       = { room_id = settings.room_id, room_name = settings.room_name },
        is_playing = is_playing,
        members    = members,
      }

    val find_user_seat(game_state : whole_game_state, user_id : user_id) =
      let opt = Quad.find(fun(p) -> Binary.equal(p.user.user_id, user_id) end, game_state.meta.players) in
      Option.map(
        fun(pair) ->
          let {seat, _} = pair in
          seat
        end,
        opt)

    val the_other_seats(seat) =
      case seat of
      | Models.SeatA -> [Models.SeatB, Models.SeatC, Models.SeatD]
      | Models.SeatB -> [Models.SeatC, Models.SeatD, Models.SeatA]
      | Models.SeatC -> [Models.SeatD, Models.SeatA, Models.SeatB]
      | Models.SeatD -> [Models.SeatA, Models.SeatB, Models.SeatC]
      end

    val the_other_players(game_state : whole_game_state, seat : Quad.seat) : list<user_id> =
      let {u0, u1, u2, u3} = Quad.decompose(Quad.map(fun(p) -> p.user.user_id end, game_state.meta.players)) in
      case seat of
      | Models.SeatA -> [u1, u2, u3]
      | Models.SeatB -> [u2, u3, u0]
      | Models.SeatC -> [u3, u0, u1]
      | Models.SeatD -> [u0, u1, u2]
      end

    val make_observable_game_state(game_state : whole_game_state, user_id : user_id) : option<observable_game_state> =
      case find_user_seat(game_state, user_id) of
      | None ->
          None

      | Some(seat) ->
          let synchronizing =
            case game_state.sync of
            | WaitingInput   -> false
            | WaitingSync(_) -> true
            end
          in
          let observable_inning =
            case game_state.inning of
            | DuringInning(inning)     -> Models.ObservableDuringInning(Inning.observe(seat, inning))
            | InningEnd(gains_quad, _) -> Models.ObservableInningEnd(gains_quad)
            end
          in
          Some({
            meta              = game_state.meta,
            observable_inning = observable_inning,
            synchronizing     = synchronizing,
            snapshot_id       = game_state.snapshot_id,
          })
      end

    val make_observable(room_state : internal_room_state, user_id : user_id) : option<observable_room_state> =
      case room_state of
      | InternalWaiting(waiting_state) ->
          let members = List.map(fun(w) -> w.user end, waiting_state.members) in
          Some(Models.WaitingStart(members))

      | InternalPlaying(game_state) ->
          Option.map(fun(obs) -> Models.PlayingGame(obs) end, make_observable_game_state(game_state, user_id))
      end

    val make_personal_state(state : state, user_id : user_id) : option<personal_state> =
      let settings = state.settings in
      case make_observable(state.room_state, user_id) of
      | None ->
          None

      | Some(observable) ->
          Some({
            room = { room_id = settings.room_id, room_name = settings.room_name },
            logs = List.reverse(state.reversed_logs),
            game = observable,
          })
      end

    val make_initial_sync() : Quad.t<bool> =
      Quad.make({false, false, false, false})

    val start_new_inning<$a>(game_meta : game_meta) : [$a]whole_game_state = act
      let next_parent_seat = game_meta.parent_seat in
      let inning = Inning.start(next_parent_seat) in
      do snapshot_id <- SnapshotId.generate() in
      return({
        meta        = game_meta,
        inning      = DuringInning(inning),
        snapshot_id = snapshot_id,
        sync        = WaitingSync(make_initial_sync()),
      })

    val warn_notification_failure(reason : RawValue.t, user_id : user_id, notifications : list<notification>) =
      assert Logger.warning(
        f'notify_log failed (reason: ~p, to: ~p, notifications: ~p)',
        {reason, user_id, notifications}
      )

    val notify<$a> : fun(user_id, list<notification>) -> [$a]unit = external 2 ```
      notify(UserId, Notifications) ->
          case tianjiupai_websocket:notify(UserId, Notifications) of
              ok ->
                  ok;
              {error, Reason} ->
                  ?MODULE:warn_notification_failure(Reason, UserId, Notifications),
                  ok
          end.
    ```

    val notify_for_each<$a>(user_ids : list<user_id>, logs : list<notification>) : [$a]unit = act
      List.for_each(fun(user_id) -> act notify(user_id, logs) end, user_ids)

    val handle_send_chat(user : user, text : binary, state : state) = act
      let {_, members} = get_members_from_state(state.room_state) in
      let comment = { from = user, text = text } in
      do dummy <-
        let user_ids = List.map(fun (u) -> u.user_id end, members) in
        notify_for_each(user_ids, [Models.NotifyComment(comment)])
      in
      GenServer.reply(ChatSent, { state | reversed_logs = Models.LogComment(comment) :: state.reversed_logs })

    val handle_attend(user : user, state : state) = act
      let user_id = user.user_id in
      case state.room_state of
      | InternalPlaying(_) ->
          GenServer.reply(Attended(None), state)

      | InternalWaiting(waiting_state) ->
          let members = waiting_state.members in
          if List.any(fun(w) -> Binary.equal(w.user.user_id, user_id) end, members) then
            let Some(personal_state) = make_personal_state(state, user_id) in
            GenServer.reply(Attended(Some(personal_state)), state)
          else
            let log_enter = Models.LogEntered(user) in
            let notif_enter = Models.NotifyEntered(user) in
            case members of
            | w0 :: w1 :: w2 :: _ ->
                let u0 = w0.user in
                let u1 = w1.user in
                let u2 = w2.user in
                let player_quad =
                  let player0 = { user = u0, is_connected = w0.is_connected } in
                  let player1 = { user = u1, is_connected = w1.is_connected } in
                  let player2 = { user = u2, is_connected = w2.is_connected } in
                  let player3 = { user = user, is_connected = true } in
                     /* TODO: specify an appropriate value for `is_connected` */
                  Quad.make({Some(player0), Some(player1), Some(player2), Some(player3)})
                in
                let parent_seat = Models.SeatA in
                let game_meta =
                  {
                    inning_index     = 0,
                    num_consecutives = 0, /* The initial value is exceptionally `0`, not `1` */
                    parent_seat      = parent_seat,
                    scores           = Quad.make({0, 0, 0, 0}),
                    players          = player_quad,
                  }
                in
                let game_index = { inning_index = 0, num_consecutives = 0 } in
                do game_state <- start_new_inning(game_meta) in
                do dummy <-
                  List.for_each(
                    fun(u) -> act
                      let Some(obs) = make_observable_game_state(game_state, u) in
                      notify(u, [notif_enter, Models.NotifyGameStart(obs)])
                    end,
                    [u0.user_id, u1.user_id, u2.user_id]
                  )
                in
                let state =
                  { state |
                    room_state    = InternalPlaying(game_state),
                    reversed_logs = Models.LogGameStart(game_index) :: log_enter :: state.reversed_logs,
                  }
                in
                let Some(personal_state) = make_personal_state(state, user_id) in
                GenServer.reply(Attended(Some(personal_state)), state)

            | _ ->
                let w = { user = user, is_connected = true } in
                  /* TODO: specify an appropriate value for `is_connected` */
                let waiting_state = { waiting_state | members = List.append(members, [w]) } in
                do dummy <- notify_for_each(List.map(fun(w) -> w.user.user_id end, members), [notif_enter]) in
                let state =
                  { state |
                    room_state    = InternalWaiting(waiting_state),
                    reversed_logs = log_enter :: state.reversed_logs,
                  }
                in
                let Some(personal_state) = make_personal_state(state, user_id) in
                GenServer.reply(Attended(Some(personal_state)), state)
            end
      end

    val handle_exit(user_id : user_id, state : state) = act
      case state.room_state of
      | InternalPlaying(_) ->
          GenServer.reply(Exited(false), state)

      | InternalWaiting(waiting_state) ->
          let members0 = waiting_state.members in
          case List.filter(fun(w0) -> Binary.equal(w0.user.user_id, user_id) end, members0) of
          | [] ->
              GenServer.reply(Exited(false), state)
          | w :: _ ->
              let user = w.user in
              let members1 =
                List.filter(
                  fun(w0) -> Bool.not(Binary.equal(w0.user.user_id, user_id)) end,
                  members0)
              in
              do dummy <-
                notify_for_each(List.map(fun(w) -> w.user.user_id end, members1), [Models.NotifyExited(user)])
              in
              let state =
                { state |
                  room_state    = InternalWaiting({ waiting_state | members = members1 }),
                  reversed_logs = Models.LogExited(user) :: state.reversed_logs,
                }
              in
              GenServer.reply(Exited(true), state)
          end
      end

    val make_winner_diff(losers : list<{Quad.seat, int}>) : int =
      let loser_diff_sum =
        List.foldl(
          fun(acc, loser) -> let {_, loser_diff} = loser in acc + loser_diff end,
          0,
          losers)
      in
      0 - loser_diff_sum

    val update_score(scores : Quad.t<int>, updates : list<{Quad.seat, int}>) : {Quad.t<int>, Quad.t<int>} =
      List.foldl(
        fun(acc, update) ->
          let {scores, diffs} = acc in
          let {seat, diff} = update in
          let score = Quad.access(seat, scores) in
          {
            Quad.update(seat, score + diff, scores),
            Quad.update(seat, diff, diffs)
          }
        end,
        {scores, Quad.make({0, 0, 0, 0})},
        updates)

    val deal_with_trick_end(
      game_meta   : game_meta,
      winner_seat : Quad.seat,
      special_opt
    ) : {game_meta, option<Quad.t<int>>} =
      case special_opt of
      | None ->
          {game_meta, None}
      | Some(special) ->
          let loser_seats = the_other_seats(winner_seat) in
          let num_consecutives = game_meta.num_consecutives in
          let parent_seat = game_meta.parent_seat in
          let losers =
            if Quad.seat_equal(winner_seat, parent_seat) then
            /* If the parent wins the trick: */
              List.map(
                fun(loser_seat) ->
                  let loser_diff = 0 - 2 * (num_consecutives + 1) in
                  {loser_seat, loser_diff}
                end,
                loser_seats)
            else
              List.map(
                fun(loser_seat) ->
                  let loser_diff =
                    if Quad.seat_equal(loser_seat, parent_seat) then
                      0 - 2 * (num_consecutives + 1)
                    else
                      0 - 2
                  in
                  {loser_seat, loser_diff}
                end,
                loser_seats)
          in
          let winner_diff = make_winner_diff(losers) in
          let {scores, diffs} = update_score(game_meta.scores, {winner_seat, winner_diff} :: losers) in
          {
            { game_meta | scores = scores },
            Some(diffs)
          }
      end

    val deal_with_inning_end(game_meta, winner_seat, gains_quad) : {game_meta, Quad.t<int>} =
      let loser_seats = the_other_seats(winner_seat) in
      let num_consecutives = game_meta.num_consecutives in
      let parent_seat = game_meta.parent_seat in
      if Quad.seat_equal(winner_seat, parent_seat) then
      /* If the parent wins the inning: */
        let losers =
          List.map(
            fun(loser_seat) ->
              let num_gains = List.length(Quad.access(loser_seat, gains_quad)) in
              let loser_diff =
                if num_gains == 0 then
                  0 - 5 * (num_consecutives + 1)
                else
                  (num_gains - 4) * (num_consecutives + 1)
              in
              {loser_seat, loser_diff}
            end,
            loser_seats)
        in
        let winner_diff = make_winner_diff(losers) in
        let {scores, diffs} = update_score(game_meta.scores, {winner_seat, winner_diff} :: losers) in
        {
          {
            inning_index     = game_meta.inning_index,
            num_consecutives = num_consecutives + 1,
            parent_seat      = parent_seat,
            scores           = scores,
            players          = game_meta.players,
          },
          diffs
        }
      else
        let losers =
          List.map(
            fun(loser_seat) ->
              let num_gains = List.length(Quad.access(loser_seat, gains_quad)) in
              let loser_diff =
                let coeff =
                  if Quad.seat_equal(loser_seat, parent_seat) then
                    num_consecutives + 1
                  else
                    1
                in
                if num_gains == 0 then
                  0 - 5 * coeff
                else
                  (num_gains - 4) * coeff
              in
              {loser_seat, loser_diff}
            end,
            loser_seats)
        in
        let winner_diff = make_winner_diff(losers) in
        let {scores, diffs} = update_score(game_meta.scores, {winner_seat, winner_diff} :: losers) in
        {
          {
            inning_index     = game_meta.inning_index + 1,
            num_consecutives = 1,
            parent_seat      = winner_seat,
            scores           = scores,
            players          = game_meta.players,
          },
          diffs
        }

    val handle_submit(user_id : user_id, cards : list<Card.t>, state : state) = act
      do opt <-
        case state.room_state of
        | InternalWaiting(_) ->
            return(None)

        | InternalPlaying(game_state) ->
            case find_user_seat(game_state, user_id) of
            | None ->
                return(None)

            | Some(seat) ->
                case game_state.inning of
                | InningEnd(_, _) ->
                    return(None)

                | DuringInning(inning) ->
                    case Inning.submit(seat, cards, inning) of
                    | None ->
                        return(None)

                    | Some({submit_result, is_front}) ->
                        do snapshot_id <- SnapshotId.generate() in
                        let game_meta = game_state.meta in
                        let {game_state, last_opt} =
                          case submit_result of
                          | Inning.Continues(inning) ->
                              {
                                {
                                  meta        = game_meta,
                                  inning      = DuringInning(inning),
                                  snapshot_id = snapshot_id,
                                  sync        = WaitingSync(make_initial_sync()),
                                },
                                None
                              }

                          | Inning.WinsTrick(winner_seat, last_table, submitter_hand, special_opt, inning) ->
                              let {game_meta, diffs_opt} = deal_with_trick_end(game_meta, winner_seat, special_opt) in
                              {
                                {
                                  meta        = game_meta,
                                  inning      = DuringInning(inning),
                                  snapshot_id = snapshot_id,
                                  sync        = WaitingSync(make_initial_sync()),
                                },
                                Some({table = last_table, hand = submitter_hand, diffs = diffs_opt})
                              }

                          | Inning.WinsInning(winner_seat, last_table, gains_quad) ->
                              let {game_meta, diffs} = deal_with_inning_end(game_meta, winner_seat, gains_quad) in
                              {
                                {
                                  meta        = game_meta,
                                  inning      = InningEnd(gains_quad, make_initial_sync()),
                                  snapshot_id = snapshot_id,
                                  sync        = WaitingSync(make_initial_sync()),
                                },
                                Some({table = last_table, hand = [], diffs = Some(diffs)})
                              }
                          end
                        in
                        let card_opts =
                          if is_front then
                            List.map(fun(card) -> Some(card) end, cards)
                          else
                            List.map(fun(card) -> None end, cards)
                        in
                        do dummy <-
                          List.for_each(
                            fun(u) -> act
                              let Some(obs) = make_observable_game_state(game_state, u) in
                              let submission =
                                {
                                  seat       = seat,
                                  submitted  = card_opts,
                                  new_state  = obs,
                                  trick_last = Option.map(fun(last) -> { table = last.table, diffs = last.diffs } end, last_opt),
                                }
                              in
                              notify(u, [Models.NotifySubmission(submission)])
                            end,
                            the_other_players(game_state, seat)
                          )
                        in
                        let Some(obs) = make_observable_game_state(game_state, user_id) in
                        return(Some({obs, last_opt, game_state}))
                    end
                end
            end
        end
      in
      case opt of
      | None ->
          GenServer.reply(SubmissionDone(None), state)

      | Some({obs, last_opt, game_state}) ->
          let reversed_logs =
            case last_opt of
            | None ->
                state.reversed_logs
            | Some(last) ->
                case last.diffs of
                | None ->
                    state.reversed_logs
                | Some(diffs) ->
                    Models.LogDiffs(diffs) :: state.reversed_logs
                end
            end
          in
          GenServer.reply(
            SubmissionDone(Some({obs, last_opt})),
            { state |
              room_state    = InternalPlaying(game_state),
              reversed_logs = reversed_logs,
            },
          )
      end

    val handle_ack(user_id : user_id, snapshot_id : snapshot_id, state : state) = act
      do opt <-
        case state.room_state of
        | InternalWaiting(_) ->
            return(None)

        | InternalPlaying(game_state) ->
            case find_user_seat(game_state, user_id) of
            | None ->
                return(None)

            | Some(seat) ->
                if SnapshotId.equal(snapshot_id, game_state.snapshot_id) then
                  case game_state.sync of
                  | WaitingSync(sync_quad) ->
                      let sync_quad = Quad.update(seat, true, sync_quad) in
                      let _ =
                        assert Logger.info(
                          f'update sync (snapshot_id: ~p, quad: ~p)',
                          {snapshot_id, sync_quad}
                        )
                      in
                      case Quad.decompose(sync_quad) of
                      | {true, true, true, true} ->
                        /* If all the players are ready for the next step: */
                          let user_ids = the_other_players(game_state, seat) in
                          do dummy <- notify_for_each(user_id :: user_ids, [Models.NotifyNextStep]) in
                          let game_state = { game_state | sync = WaitingInput } in
                          return(Some(game_state))

                      | _ ->
                          let game_state = { game_state | sync = WaitingSync(sync_quad) } in
                          return(Some(game_state))
                      end
                  | WaitingInput ->
                      let _ =
                        assert Logger.warning(
                          f'waiting input (snapshot_id: ~p)',
                          {snapshot_id}
                        )
                      in
                      return(None)
                  end
                else
                  let _ =
                    assert Logger.warning(
                      f'snapshot mismatch (got: ~p, expected: ~p)',
                      {snapshot_id, game_state.snapshot_id}
                    )
                  in
                  return(None)
            end
        end
      in
      case opt of
      | None             -> GenServer.no_reply(state)
      | Some(game_state) -> GenServer.no_reply({ state | room_state = InternalPlaying(game_state) })
      end

    val handle_require_next_inning(user_id : user_id, snapshot_id : snapshot_id, state : state) = act
      do opt <-
        case state.room_state of
        | InternalWaiting(_) ->
            return(None)

        | InternalPlaying(game_state) ->
            case find_user_seat(game_state, user_id) of
            | None ->
                return(None)

            | Some(seat) ->
                if SnapshotId.equal(snapshot_id, game_state.snapshot_id) then
                  case game_state.inning of
                  | DuringInning(_) ->
                      return(None)

                  | InningEnd(gains_quad, inning_sync_quad) ->
                      let inning_sync_quad = Quad.update(seat, true, inning_sync_quad) in
                      let _ =
                        assert Logger.info(
                          f'update inning sync (snapshot_id: ~p, inning_sync_quad: ~p)',
                          {snapshot_id, inning_sync_quad}
                        )
                      in
                      case Quad.decompose(inning_sync_quad) of
                      | {true, true, true, true} ->
                        /* If all the players are ready for the next step: */
                          do game_state <- start_new_inning(game_state.meta) in
                          let user_ids = the_other_players(game_state, seat) in
                          do dummy <-
                            List.for_each(
                              fun(u) -> act
                                let Some(obs) = make_observable_game_state(game_state, u) in
                                notify(u, [Models.NotifyGameStart(obs)])
                              end,
                              user_id :: user_ids)
                          in
                          let game_index =
                            let meta = game_state.meta in
                            {
                              inning_index     = meta.inning_index,
                              num_consecutives = meta.num_consecutives,
                            }
                          in
                          return(Some({game_state, Some(game_index)}))

                      | _ ->
                          let game_state = { game_state | inning = InningEnd(gains_quad, inning_sync_quad) } in
                          return(Some({game_state, None}))
                      end
                  end
                else
                  return(None)
            end
        end
      in
      case opt of
      | None ->
          GenServer.no_reply(state)
      | Some({game_state, game_index_opt}) ->
          let state =
            case game_index_opt of
            | None             -> state
            | Some(game_index) -> { state | reversed_logs = Models.LogGameStart(game_index) :: state.reversed_logs }
            end
          in
          GenServer.no_reply({ state | room_state = InternalPlaying(game_state) })
      end

    val handle_set_connection_status(user_id : user_id, is_connected : bool, state : state) = act
      let opt =
        case state.room_state of
        | InternalWaiting(waiting_state) ->
            let members0 = waiting_state.members in
            case List.filter(fun(w) -> Binary.equal(w.user.user_id, user_id) end, members0) of
            | [] ->
                None

            | w :: _ ->
                let user = w.user in
                let members1 =
                  List.map(
                    fun(w) ->
                      if Binary.equal(w.user.user_id, user_id) then
                        { w | is_connected = is_connected }
                      else
                        w
                    end,
                    members0,
                  )
                in
                let user_ids_for_notification =
                  List.filter_map(
                    fun(w) ->
                      if w.is_connected then
                        let u = w.user.user_id in
                        if Binary.equal(u, user_id) then None else Some(u)
                      else
                        None
                    end,
                    members1)
                in
                Some({user, user_ids_for_notification, InternalWaiting({ members = members1 })})
            end

        | InternalPlaying(game_state) ->
            case find_user_seat(game_state, user_id) of
            | None ->
                None

            | Some(seat) ->
                let game_meta = game_state.meta in
                let players0 = game_meta.players in
                let p0 = Quad.access(seat, players0) in
                let p1 = { p0 | is_connected = is_connected } in
                let players1 = Quad.update(seat, p1, players0) in
                let user_ids_for_notification =
                  List.filter_map(
                    fun(g) ->
                      if g.is_connected then
                        let u = g.user.user_id in
                        if Binary.equal(u, user_id) then None else Some(u)
                      else
                        None
                    end,
                    Quad.to_list(players1)
                  )
                in
                let game_state = { game_state | meta = { game_meta | players = players1 } } in
                Some({p1.user, user_ids_for_notification, InternalPlaying(game_state)})
            end
        end
      in
      case opt of
      | None ->
          GenServer.no_reply(state)

      | Some({user, user_ids_for_notification, room_state}) ->
          let connection = { user = user, is_connected = is_connected } in
          do dummy <- notify_for_each(user_ids_for_notification, [Models.NotifyConnection(connection)]) in
          let state =
            { state |
              room_state    = room_state,
              reversed_logs = Models.LogConnection(connection) :: state.reversed_logs,
            }
          in
          GenServer.no_reply(state)
      end

    val update_sync(user_id : user_id, state : state) = act
      case state.room_state of
      | InternalWaiting(_) ->
          return(state)

      | InternalPlaying(game_state) ->
          case find_user_seat(game_state, user_id) of
          | None ->
              return(state)

          | Some(seat) ->
              case game_state.sync of
              | WaitingInput ->
                  return(state)

              | WaitingSync(sync_quad) ->
                  let sync_quad = Quad.update(seat, true, sync_quad) in
                  case Quad.decompose(sync_quad) of
                  | {true, true, true, true} ->
                    /* If all the players are ready for the next step: */
                      let user_ids = the_other_players(game_state, seat) in
                      do dummy <- notify_for_each(user_ids, [Models.NotifyNextStep]) in
                      let game_state = { game_state | sync = WaitingInput } in
                      return({ state | room_state = InternalPlaying(game_state) })

                  | _ ->
                      let game_state = { game_state | sync = WaitingSync(sync_quad) } in
                      return({ state | room_state = InternalPlaying(game_state) })
                  end
              end
          end
      end

    val handle_call(req, from, state) = act
      case req of
      | GetWholeState ->
          let whole_state = make_whole_room_state(state) in
          GenServer.reply(WholeState(whole_state), state)
      | GetPersonalState(user_id) ->
          do state <- update_sync(user_id, state) in
          let personal_state_opt = make_personal_state(state, user_id) in
          GenServer.reply(PersonalState(personal_state_opt), state)
      | SendChat(user, text) ->
          handle_send_chat(user, text, state)
      | Attend(user) ->
          handle_attend(user, state)
      | Exit(user_id) ->
          handle_exit(user_id, state)
      | Submit(user_id, cards) ->
          handle_submit(user_id, cards, state)
      end

    val handle_cast(msg, state) = act
      case msg of
      | Ack(user_id, snapshot_id) ->
          handle_ack(user_id, snapshot_id, state)

      | RequireNextInning(user_id, snapshot_id) ->
          handle_require_next_inning(user_id, snapshot_id, state)

      | SetConnectionStatus(user_id, is_connected) ->
          handle_set_connection_status(user_id, is_connected, state)
      end

    val handle_info(info, state) = act
      let _ = assert Logger.warning(f'unexpected info (info: ~p, state: ~p)', {info, state}) in
      GenServer.no_reply(state)

    val terminate(state) = act
      let _ = assert Logger.info(f'terminate (room_id: ~p)', {state.settings.room_id}) in
      return({})
  end

  module Impl = GenServer.Make(Callback)

  type info = Callback.info

  type proc = Impl.proc

  val monitor_impl<$a> : fun(pack sig end, room_id) -> [$a]option<RawValue.t> = external 2 ```
    monitor_impl(Impl, RoomId) ->
        case Impl:where_is_global(RoomId) of
            error     -> error;
            {ok, Pid} -> {ok, erlang:monitor(process, Pid)}
        end.
  ```

  val monitor(room_id) = act
    monitor_impl(pack Impl : sig end, room_id)

  val start_link<$a>(room_id : room_id, room_name : binary) : [$a]result<proc, GenServer.start_link_error> = act
    Impl.start_link_name({room_id, room_name}, -name Global(room_id))

  val as_pid(proc) =
    Impl.as_pid(proc)

  val from_pid(proc) =
    Impl.from_pid(proc)

  val call<$a, $ret>(
    room_id : room_id,
    req     : request_impl,
    f       : fun(response_impl) -> [$a]option<$ret>
  ) : [$a]option<$ret> = act
    do proc_opt <- Impl.where_is_global(room_id) in
    case proc_opt of
    | None ->
        return(None)

    | Some(proc) ->
        do resp <- Impl.call(proc, req) in
        f(resp)
    end

  val get_whole_state<$a>(room_id : room_id) : [$a]option<room_summary> = act
    call(room_id, GetWholeState, fun(resp) -> act
      case resp of
      | WholeState(whole_state) -> return(Some(whole_state))
      end
    end)

  val get_whole_state_by_proc<$a>(proc : proc) : [$a]option<room_summary> = act
    do resp <- Impl.call(proc, GetWholeState) in
    case resp of
    | WholeState(whole_state) -> return(Some(whole_state))
    end

  val get_personal_state<$a>(room_id : room_id, user_id : user_id) : [$a]option<personal_state> = act
    call(room_id, GetPersonalState(user_id), fun(resp) -> act
      case resp of
      | PersonalState(personal_state_opt) -> return(personal_state_opt)
      end
    end)

  val send_chat<$a>(room_id : room_id, user : user, text : binary) : [$a]option<unit> = act
    call(room_id, SendChat(user, text), fun(resp) -> act
      case resp of
      | ChatSent -> return(Some({}))
      end
    end)

  val attend<$a>(room_id : room_id, user : user) : [$a]option<personal_state> = act
    call(room_id, Attend(user), fun(resp) -> act
      case resp of
      | Attended(game_state_opt) -> return(game_state_opt)
      end
    end)

  val exit<$a>(room_id, user_id) : [$a]option<unit> = act
    call(room_id, Exit(user_id), fun(resp) -> act
      case resp of
      | Exited(false) -> return(None)
      | Exited(true)  -> return(Some({}))
      end
    end)

  val submit<$a>(room_id, user_id, cards) : [$a]option<{observable_game_state, option<last>}> = act
    call(room_id, Submit(user_id, cards), fun(resp) -> act
      case resp of
      | SubmissionDone(v) -> return(v)
      end
    end)

  val ack<$a>(room_id, user_id, snapshot_id) : [$a]unit = act
    do proc_opt <- Impl.where_is_global(room_id) in
    case proc_opt of
    | None ->
        return({})

    | Some(proc) ->
        Impl.cast(proc, Callback.Ack(user_id, snapshot_id))
    end

  val require_next_inning<$a>(room_id, user_id, snapshot_id) : [$a]unit = act
    do proc_opt <- Impl.where_is_global(room_id) in
    case proc_opt of
    | None ->
        return({})

    | Some(proc) ->
        Impl.cast(proc, Callback.RequireNextInning(user_id, snapshot_id))
    end

  val set_connection(room_id, user_id, is_connected) = act
    do proc_opt <- Impl.where_is_global(room_id) in
    case proc_opt of
    | None       -> return({})
    | Some(proc) -> Impl.cast(proc, Callback.SetConnectionStatus(user_id, is_connected))
    end

  val notify_connected(room_id, user_id) = act
    set_connection(room_id, user_id, true)

  val notify_disconnected(room_id, user_id) = act
    set_connection(room_id, user_id, false)

end
